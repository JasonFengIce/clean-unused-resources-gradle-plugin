plugins {
    id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'groovy'

def versionName = '0.1.0'
group 'com.youzan.mobile'
version versionName


repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

apply plugin: 'maven-publish'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

Properties properties = new Properties();
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'cleaner-gradle-plugin'
        desc = 'a gradle plugin to clean unused resources detected by Lint'
        websiteUrl = 'https://github.com/YouzanMobile/clean-resource-gradle-plugin'
        issueTrackerUrl = 'https://github.com/YouzanMobile/clean-resource-gradle-plugin/issues'
        vcsUrl ='https://github.com/YouzanMobile/clean-resource-gradle-plugin'
        publicDownloadNumbers = true
        licenses = ['MIT']
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.youzan.mobile'
            artifactId 'cleaner-gradle-plugin'
            version versionName
        }
    }
}
